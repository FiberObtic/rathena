-	script	CfgGSpin_1	-1,{
	end;
	
OnInit:
	// รหัสไอเท็มที่ใช้สุ่มของ
	set $@GSS_req_item,30001;
	//จำนวนไอเท็มที่ใช้สุ่มของ
	set $@GSS_req_item_amount,39;
	// ตั้งค่ารูปภาพ slot
	set $@GSS_slot_default_filename$,"slot_lv7_00";
	setarray $@GSS_slot_filename$[0],
		"slot_lv7_00", "slot_lv7_01", "slot_lv7_03", "slot_lv7_04", "slot_lv7_05", "slot_lv7_06",
		"slot_lv7_07", "slot_lv7_08", "slot_lv7_09", "slot_lv7_10", "slot_lv7_11", "slot_lv7_12",
		"slot_lv7_13", "slot_lv7_14", "slot_lv7_15", "slot_lv7_16", "slot_lv7_17", "slot_lv7_18";
	// ตั้งค่าไอเท็มของรางวัล	
	setarray $@GSS_slot_item[0],
		501, 502, 503, 504, 505, 506,
		501, 502, 503, 504, 505, 506,
		501, 502, 503, 504, 505, 506;
	// ตั้งค่าจำนวนของรางวัล
	setarray $@GSS_slot_count[0],
		1, 1, 1, 1, 1, 1,
		3, 1, 1, 1, 1, 1,
		1, 1, 1, 1, 1, 1;
	// ตั้งค่าโอกาสสุ่มไอเท็ม
	// 100 คือ โอกาสออกง่ายมากๆ
	// 30-70 คือ โอกาสออกกลางๆ
	// 1-15 คือ โอกาสออกยากมากๆ
	setarray $@GSS_slot_item_rate[0],
		100, 100, 80, 100, 100, 10,
		100, 80, 100, 30, 100, 30,
		50, 80, 10, 100, 50, 50;
	// system
	setarray $@GSS_size_db[0],
		getarraysize($@GSS_slot_filename$);
	end;
}

function	script	GSS_NextSlot	{
	set .@img,getarg(0) + 1;
	
	if (.@img >= $@GSS_size_db[0])
		set .@img,0;
		
	cutin $@GSS_slot_filename$[.@img],3;
	sleep2 getarg(1);
	
	return .@img;
}

morocc,119,102,4	script	Gashapon Lucky V.1#NPC2	562,{
	
	set .@npc$,"[ "+strnpcinfo(1)+" ]";
	mes .@npc$;
	mes "สวัสดี [^FF0000 "+strcharinfo(0)+" ^000000]";
	mes "คุณต้องการเสี่ยงโชคหรือไม่";
	next;
	if (select("~ ใช่,ต้องการเสี่ยงโชค", "~ เปล่า,ฉันไม่มีดวง !") == 2) goto L_Cancel;
	mes .@npc$;
	mes "คุณต้องมีไอเท็ม";
	mes "^FF0000"+getitemname($@GSS_req_item)+"^000000 จำนวน ^FF0000"+$@GSS_req_item_amount+"^000000 ea";
	mes "ใช้สำหรับการเสี่ยงโชคครั้งนี้ !";
	next;
	switch(select("~ ตัวอย่างของรางวัล:~ ^FF0000เล่นกิจกรรม^000000 ใช้ ^0000FF"+ getitemname($@GSS_req_item) +"^000000 "+$@GSS_req_item_amount+" ea:~ ยกเลิก,กลัวหมดตัว"))
	{
		case 1:
			cutin $@GSS_slot_default_filename$,3;
		end;
		case 2:
			if ( countitem($@GSS_req_item) < $@GSS_req_item_amount )
			{
				mes .@npc$;
				mes "คุณมีไอเท็ม";
				mes "- ^FF0000"+getitemname($@GSS_req_item)+"^000000";
				mes "ไม่เพียงพอ อดเล่นกิจกรรมเลย !";
				close;
			}
			delitem $@GSS_req_item, $@GSS_req_item_amount;
			goto L_Spin;
						
		case 3: goto L_Cancel;
	}
	end;
	
L_Spin:
	mes .@npc$;
	mes "";
	mes "โปรดรอสักครู่....";
	mes "";
	mes "ระบบกำลังสุ่มไอเท็ม";
	mes "";
	mes "^FF0000*ห้ามกดปิดหน้าต่าง เด็ดขาด^000000";
	pcblockmove getcharid(3),1;
	
	set .@img,0;
	set .@round,rand(5,10); // ตั้งค่าจำนวนการสุ่ม 5,10 วินาที
	set .@speed,10;
	
	for (set .@i,0; .@i < .@round; set .@i,.@i+1)
	{
		set .@mini_round,rand($@GSS_size_db[0]/2, $@GSS_size_db[0]);
		for (set .@j,0; .@j < .@mini_round; set .@j,.@j+1)
			set .@img,callfunc("GSS_NextSlot", .@img, .@speed);
		set .@speed,.@speed+10;
	}

	set .@round,rand($@GSS_size_db[0]/2, $@GSS_size_db[0]);
	
	for (set .@i,0; .@i < .@round; set .@i,.@i+1)
	{
		set .@img,callfunc("GSS_NextSlot", .@img, .@speed);
		set .@speed,.@speed+50;
	}

	set .@min,($@GSS_size_db[0]/2)/2;
	set .@round,rand(.@min, $@GSS_size_db[0]/2);
	set .@round_per_50,.@round/2;
	set .@round_per_25,.@round_per_50/2;

	for (set .@i,0; .@i < .@round; set .@i,.@i+1)
	{
		set .@img,callfunc("GSS_NextSlot", .@img, .@speed);

		if (.@i < .@round_per_25)
			set .@speed,.@speed+25;
		else if (.@i < .@round_per_50)
			set .@speed,.@speed+100;
		else
			set .@speed,.@speed+250;
			
		if(.@i == .@round-1 && $@GSS_slot_item_rate[.@img] < rand(100)){
			set .@i,.@i-1;
		}
	}
	
	pcblockmove getcharid(3),0;
	set .@prize,$@GSS_slot_item[.@img];
	set .@count,$@GSS_slot_count[.@img];
	
	if (.@prize == 0)
	{
		mes .@npc$;
		mes "คุณไม่ได้รับของรางวัล";
		next;
		goto L_Cancel;
	}
	
	getitem .@prize, .@count;
	announce "[ Lucky Gashapon: คุณ "+strcharinfo(0)+" ได้รับไอเท็ม "+getitemname(.@prize)+" จำนวน "+ .@count +" ea. ]",bc_all,0x00FA9A;
	close2;
	cutin "", 255;
	end;
	
L_Cancel:
	mes .@npc$;
	mes "โอกาสหน้าเชิญใหม่";
	close2;
	cutin "", 255;
end;

	OnInit:
	waitingroom "JACKPOT CASH",0;
	end;
}
