//===== rAthena Script =======================================
//= Durexz Hat Shop
//===== By: ==================================================
//= Durexz
//===== Current Version: =====================================
//= 1.6c
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= A dynamic quest shop based on Lunar's, with easier config.
//= Includes support for multiple shops & cashpoints.
//= Item Preview script by mobkung.

morocc,120,82,6	script	Hat Craft#1	798,{
	select(" > ^006600คราฟหมวก^000000"," > ^006600คราฟหู^000000"," > ^006600คราฟปาก^000000","^FF0000> ออก < ^000000");
	switch(@menu){
		case 1:
			callfunc "hshop",1;
			break;
		case 2:
			callfunc "hshop",2;
			break;
		case 3:
			callfunc "hshop",3;
			break;
		default:
			break;
	}
	end;
OnInit:
	waitingroom "คราฟหมวก",0;
	end;
}
// prontera,165,203,6	script	Quest Shop#2	998,{ callfunc "qshop",1,2; }	// call the shop 1 and 2 defined below
// etc.. Add your Shop NPCs 'Quest Shop#XXX' here
//============================================================


// Script Core - DO NOT DUPLICATE THIS NPC !!!!!!!!!!!!!
//============================================================
-	script	hats_shop	-1,{
function Add; function Chk; function Slot;
OnInit:
	freeloop(1);

// -----------------------------------------------------------
//  Basic shop settings.
// -----------------------------------------------------------

	set .Announce,1;	// Announce quest completion? (1: yes / 0: no)
	set .ShowSlot,1;	// Show item slots? (2: all equipment / 1: if slots > 0 / 0: never)
	set .ShowID,1;  	// Show item IDs? (1: yes / 0: no)
	set .ShowZeny,1;	// Show Zeny cost, if any? (1: yes / 0: no)
	set .MaxStack,1;	// Max number of quest items purchased at one time.
// -----------------------------------------------------------
//  Points variable -- optional quest requirement.
//	setarray .Points$[0],"<variable name>","<display name>";
// -----------------------------------------------------------

	setarray .Points$[0],
		"#CASHPOINTS", "Cash Points";


//=====================================================================================
// ------------------- ADD YOUR SHOPS NAME AND ITEMS SHOPS STARTING HERE --------------
//=====================================================================================

// -----------------------------------------------------------
//  Shop IDs -- to add shops, copy dummy data at bottom of file.
//	setarray .Shops$[1],"<Shop 1>","<Shop 2>"{,...};
// -----------------------------------------------------------

	setarray .Shops$[1],
		"Upper Head Gear Shop",		// Shop Named 1
		"Middle Head Gear Shop",		// Shop Named 2
		"Lower Head Gear Shop",		// Shop Named 3
		"Socket Shop";		// Shop Named 4

// -----------------------------------------------------------
//  Quest items -- do NOT use a reward item more than once!
//	Add(<shop ID>,<reward ID>,<reward amount>,
//	    <Zeny cost>,<point cost>,
//	    <required item ID>,<required item amount>{,...});
// -----------------------------------------------------------


//==================== Shop 1 ====================
	Add(1,2284,1,10,0,923,20);
	Add(1,5057,1,10000,0,2213,1,983,1,914,200);
	Add(1,5016,1,10,0,1030,10);
	Add(1,2214,1,10,0,949,100,706,1,722,1,2213,1);
	Add(1,5028,1,10,0,921,300);
	Add(1,2283,1,5000,0,724,1,5001,1,949,200);
	Add(1,2280,1,10000,0,1019,120);
	
//==================== Shop 2 ====================	
	Add(2,5089,1,10,0,1095,3000,2288,1);
	Add(2,5175,1,100000,0,7111,100,938,99,983,1);
	Add(2,2296,1,50000,0,2243,1,999,100);
	
//==================== Shop 3 ====================	
	Add(3,5110,1,10,0,526,2,7270,1,941,1,10004,1);
	Add(3,5107,1,10,0,519,50,7031,50,548,50,539,50);
	

//=====================================================================================
// ------------------- YOUR SHOPS AND ITEMS SHOPS HAVE BEEN ADDED ---------------------
//=====================================================================================

	freeloop(0);
	set .menu$,"";
	for(set .@i,1; .@i<=getarraysize(.Shops$); set .@i,.@i+1) {
		set .menu$, .menu$+.Shops$[.@i]+":";
		npcshopdelitem "hshop"+.@i,909;
	}
	end;

OnMenu:
	set .@size, getarraysize(@i);
	if (!.@size) set @shop_index, select(.menu$);
	else if (.@size == 1) set @shop_index, @i[0];
	else {
		for(set .@j,0; .@j<.@size; set .@j,.@j+1)
			set .@menu$, .@menu$+.Shops$[@i[.@j]]+":";
		set @shop_index, @i[select(.@menu$)-1];
	}
	deletearray @i[0],getarraysize(@i);
	if (.Shops$[@shop_index] == "") {
		message strcharinfo(0),"An error has occurred.";
		end;
	}
	dispbottom "ซื้อได้ทีละ 1 ชิ้น.";
	callshop "hshop"+@shop_index,1;
	npcshopattach "hshop"+@shop_index;
	end;

OnBuyItem:
	// .@q[] : RewardID, BoughtAmt, RewardAmt, BaseAmt, ReqZeny, ReqPts, { ReqItem, ReqAmt, ... }
	setarray .@q[0],@bought_nameid[0],((@bought_quantity[0] > .MaxStack)?.MaxStack:@bought_quantity[0]);
	copyarray .@q[3],getd(".h_"+@shop_index+"_"+.@q[0]+"[0]"),getarraysize(getd(".h_"+@shop_index+"_"+.@q[0]));
	set .@q[2],.@q[1]*.@q[3];
	if (!.@q[2] || .@q[2] > 30000) {
		message strcharinfo(0),"ซื้อมากขนาดนั้นไม่ได้ ไอเท็มคือ "+getitemname(.@q[0])+".";
		end;
	}
	mes "[Quest Hat]";
	mes "รางวัล: ^0055FF"+((.@q[2] > 1)?.@q[2]+"x ":"")+Slot(.@q[0])+"^000000";
	disable_items;
	next;
	setarray @qe[1], getiteminfo(.@q[0], ITEMINFO_LOCATIONS), getiteminfo(.@q[0], ITEMINFO_VIEW);
	if (@qe[2] > 0 && ((@qe[1] & EQP_HEAD_LOW) || (@qe[1] & EQP_HEAD_TOP) || (@qe[1] & EQP_HEAD_MID) || (@qe[1] & EQP_COSTUME_HEAD_TOP) || (@qe[1] & EQP_COSTUME_HEAD_MID) || (@qe[1] & EQP_COSTUME_HEAD_LOW) || (@qe[1] & EQP_GARMENT) || (@qe[1] & EQP_COSTUME_GARMENT)))
		set .@preview,1;
	addtimer 1000, strnpcinfo(0)+"::OnEnd";
	while(1) {
		switch(select(" ~ แลก ^0055FF"+getitemname(.@q[0])+"^000000:"+((.@preview && !@qe[7])?" ~ ทดลองสวมใส่...":"")+": ~ ^777777ยกเลิก^000000")) {
		case 1:
			if (@qe[0]) {
				mes "[Quest Hat]";
				mes "ไอเท็มที่ใช้ในการแลกไม่เพียงพอ";
				close;
			}
			if (!checkweight(.@q[0],.@q[2])) {
				mes "[Quest Hat]";
				mes "^FF0000ต้องการน้ำหนักจำนวน "+(((.@q[2]*getiteminfo(.@q[0], ITEMINFO_WEIGHT))+Weight-MaxWeight)/10)+" สำหรับการแลกเปลี่ยนครั้งนี้.^000000";
				close;
			}
			next;
			@qe[0] = 0;
			mes "[Quest Shop]";
			mes "รางวัล: ^0055FF"+((.@q[2] > 1)?.@q[2]+"x ":"")+Slot(.@q[0])+"^000000";
			mes "ความต้องการ:";
			if (.@q[4]) mes " > "+Chk(Zeny,.@q[4]*.@q[1])+(.@q[4]*.@q[1])+" Zeny^000000";
			if (.@q[5]) mes " > "+Chk(getd(.Points$[0]),.@q[5]*.@q[1])+(.@q[5]*.@q[1])+" "+.Points$[1]+" ("+getd(.Points$[0])+"/"+(.@q[5]*.@q[1])+")^000000";
			if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
				mes " > "+Chk(countitem(.@q[.@i]),.@q[.@i+1]*.@q[1])+((.ShowID)?"{"+.@q[.@i]+"} ":"")+"<ITEM>"+Slot(.@q[.@i])+"<INFO>"+.@q[.@i]+"</INFO></ITEM>"+" ("+countitem(.@q[.@i])+"/"+(.@q[.@i+1]*.@q[1])+")^000000";
			mes "ท่านตกลงหรือไม่";
			if(select("ตกลง:ไม่ตกลง") == 2 ) end;
			next;	
			if(@qe[0]) {
				mes "[Quest Shop]";
				mes "ท่านนำของมาไม่ครบ";
				end;
			}
			if (.@q[4]) 
				set Zeny, Zeny-(.@q[4]*.@q[1]);
			if (.@q[5]) 
				setd .Points$[0], getd(.Points$[0])-(.@q[5]*.@q[1]);
			if (.@q[6]) 
				for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
					delitem .@q[.@i],.@q[.@i+1]*.@q[1];
			getitem .@q[0],.@q[1];
			close;
		case 2:
			setarray @qe[3], getlook(LOOK_HEAD_BOTTOM), getlook(LOOK_HEAD_TOP), getlook(LOOK_HEAD_MID), getlook(LOOK_ROBE), 1;
			if ((@qe[1] & 1) || (@qe[1] & 4096)) changelook LOOK_HEAD_BOTTOM, @qe[2];
			else if ((@qe[1] & 256) || (@qe[1] & 1024)) changelook LOOK_HEAD_TOP, @qe[2];
			else if ((@qe[1] & 512) || (@qe[1] & 2048)) changelook LOOK_HEAD_MID, @qe[2];
			else if ((@qe[1] & 4) || (@qe[1] & 8192)) changelook LOOK_ROBE, @qe[2];
			break;
		case 3:
			close;
		}
	}

OnEnd:
	if (@qe[7]) {
		changelook LOOK_HEAD_BOTTOM, @qe[3];
		changelook LOOK_HEAD_TOP, @qe[4];
		changelook LOOK_HEAD_MID, @qe[5];
		changelook LOOK_ROBE, @qe[6];
	}
	deletearray @qe[0],8;
	end;

function Add {
	if (getitemname(getarg(1)) == "null") {
		debugmes "Hat reward #"+getarg(1)+" invalid (skipped).";
		return;
	}
	setarray .@j[0],getarg(2),getarg(3),getarg(4);
	for(set .@i,5; .@i<getargcount(); set .@i,.@i+2) {
		if (getitemname(getarg(.@i)) == "null") {
			debugmes "Hat requirement #"+getarg(.@i)+" invalid (skipped).";
			return;
		} else
			setarray .@j[.@i-2],getarg(.@i),getarg(.@i+1);
	}
	copyarray getd(".h_"+getarg(0)+"_"+getarg(1)+"[0]"),.@j[0],getarraysize(.@j);
	npcshopadditem "hshop"+getarg(0),getarg(1),((.ShowZeny)?getarg(3):0);
	return;
}

function Chk {
	if (getarg(0) < getarg(1)) {
		set @qe[0],1;
		return "^FF0000";
	} else
		return "^018501";
}

function Slot {
	set .@s$,getitemname(getarg(0));
	switch(.ShowSlot) {
		case 1: if (!getitemslots(getarg(0))) return .@s$;
		case 2: if (getiteminfo(getarg(0), ITEMINFO_TYPE) == 4 || getiteminfo(getarg(0), ITEMINFO_TYPE) == 5) return .@s$+" ["+getitemslots(getarg(0))+"]";
		default: return .@s$;
	}
}
}

function	script	hshop	{
	deletearray @i[0],getarraysize(@i);
	for(set .@i,0; .@i<getargcount(); set .@i,.@i+1)
		set @i[.@i],getarg(.@i);
	doevent "hats_shop::OnMenu";
	end;
}


// Dummy shop data -- copy as needed.
//============================================================
-	shop	hshop1	-1,909:-1
-	shop	hshop2	-1,909:-1
-	shop	hshop3	-1,909:-1