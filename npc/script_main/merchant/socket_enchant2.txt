//===== rAthena Script =======================================
//= Episode 11.1 Socket Enchant NPC
//===== By: ==================================================
//= rAthena Dev Team
//===== Current Version: =====================================
//= 2.0a
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Adds slots to selected weapons and armor.
//===== Additional Comments: =================================
//= 0.1a added missing L_No: to 2 funcs [Lupus]
//= 0.1b Removed duplicates [Toms]
//= 0.2 Added missing next;'s [Evera]
//= 0.3 Fixed Zweihander[+] id [Lupus]
//= 1.0 Fixed ingredients check [Lupus]
//= 1.1 Updated to official. [SinSloth]
//= 1.1b Optimized/cleaned [ultramage]
//= 1.2 Fixed some typos and a nasty bug. [SinSloth]
//= 1.3 Fixed a wrong item ID with Sphinx Hat. [SinSloth]
//= 1.4 Corrected NPC names to fall within proper restrictions. [L0ne_W0lf]
//= 1.5 Removed Ontouch trigger area from NPCs. [L0ne_W0lf]
//= 1.6 Replaced effect numerics with constants. [Samuray22]
//= 1.7 Adjusted prices on some armor and weapons. [L0ne_W0lf]
//= 1.8 Added missing Gae Bolg and Dragon Slayer. [Kisuka]
//= 1.9 Moved Episode 12 items to separate NPC.
//=     Some cleanup & optimization. [Gepard]
//= 2.0 Duplicates now spawn from floating NPCs. [L0ne_W0lf]
//= 2.0a Added 'disable_items' command. [Euphy]
//============================================================

-	script	Leablem#dummy::SocketEnchant2	-1,{
	if (checkweight(1201,1) == 0)
	{
		mes "- รอก่อน !! -";
		mes "- กระเป๋าของเจ้า -";
		mes "- มีของมากเกินไป. -";
		mes "- ได้โปรดลอง -";
		mes "- ลดจำนวนของลงก่อน. -";
		close;
	}
	mes "[Leablem]";
	mes "ข้าคือช่างอัจฉริยะที่สามารถเจาะรูบนอาวุธและเกราะได้.";
	mes "ค่าใช้จ่ายในการทำงานของข้า, ขอเพียงแค่แร่และโอกาสสำเร็จของไอเท็มเท่านั้นพอ.";
	next;
	mes "[Leablem]";
	mes "การเจาะรูอาจดูเหมือนง่าย, แต่มันก็ซับซ้อนกว่าที่คิดน่ะ.";
	mes "ถ้าเจ้าสนใจบริการของข้า, ให้บอกข้า.";
	next;
	switch(select("เจาะรูอาวุธ:เจาะรูเกราะ:อะไรบางอย่างที่ข้าไม่อยากยุ่งด้วย:ยกเลิก"))
	{
		case 1:
			mes "[Leablem]";
			mes "เจ้าอยากเจาะรูอาวุธหรอ?";
			mes "ข้าได้แบ่งระดับความยากของการเจาะรูเอาไว้, C Class ง่ายสุดและ S Class โครตยากเลย.";
			mes "เจ้าอยากจะทำในคลาสไหนล่ะ ?";
			next;
			switch(select("C Class:B Class:A Class:S Class"))
			{
				case 1:
					mes "[Leablem]";
					mes "C Class, เอ้อ ?";
					mes "ตอนนี้, ข้าสามารถอัพเกรดอุปกรณ์บางอย่างในคลาสนี้ได้, เพราะงั้นเลือกของมาหนึ่งชิ้น, ที่เจ้าต้องการจะเจาะรู.";
					next;
					switch(select("Book of Mother Earth:Book of Billows:Book of Gust of Wind:Book of the Blazing Sun"))
					{
						case 1: callfunc "Func_Socket2",1554,1569,40,66,200,1010,10;
						case 2: callfunc "Func_Socket2",1553,1568,40,66,200,1010,10;
						case 3: callfunc "Func_Socket2",1556,1571,40,66,200,1010,10;
						case 4: callfunc "Func_Socket2",1555,1570,40,66,200,1010,10;
					}
				case 2:
					mes "[Leablem]";
					mes "B Class?";
					mes "ได้เลย, มันเป็นของสวมใส่ที่ค่อนข้างโอเคน่ะ.";
					mes "เลือกอาวุธที่เจ้าต้องการจะเจาะรูได้เลย.";
					next;
					switch(select("Orcish Axe:Scimiter:Spike"))
					{
						case 1: callfunc "Func_Socket2",1304,1309,40,66,200,984,1;
						case 2: callfunc "Func_Socket2",1113,1114,40,66,200,984,1;
						case 3: callfunc "Func_Socket2",1523,1538,40,61,300,984,1;
					}
				case 3:
					mes "[Leablem]";
					mes "อูวว, A Class.";
					mes "นี่ค่อนข้างแพงเอาการเลย!";
					mes "ได้เลย, เลือกอาวุธที่เจ้าต้องการจะเจาะรูสิ ?";
					next;
					switch(select("Dragon Killer:Katar of Quaking:Katar of Raging Blaze:Katar of Frozen Icicle:Katar of Piercing Wind:Golden Mace:Oriental Lute:Queen's Whip:Spectral Spear:Gae Bolg:Schweizersabel"))
					{
						case 1:  callfunc "Func_Socket2",13001,13030,40,61,500,984,2;
						case 2:  callfunc "Func_Socket2", 1257, 1276,40,66,500,984,2;
						case 3:  callfunc "Func_Socket2", 1258, 1277,40,66,500,984,2;
						case 4:  callfunc "Func_Socket2", 1256, 1275,40,66,500,984,2;
						case 5:  callfunc "Func_Socket2", 1259, 1278,40,66,500,984,2;
						case 6:  callfunc "Func_Socket2", 1524, 1539,40,61,500,984,2;
						case 7:  callfunc "Func_Socket2", 1918, 1922,40,61,500,984,2;
						case 8:  callfunc "Func_Socket2", 1970, 1976,40,61,500,984,2;
						case 9:  callfunc "Func_Socket2", 1477, 1479,40,56,500,984,2;
						case 10: callfunc "Func_Socket2", 1474, 1480,40,56,500,984,2;
						case 11: callfunc "Func_Socket2", 1167, 1178,40,61,500,984,2;
					}
				case 4:
					mes "[Leablem]";
					mes "ว้าว, ว้าว, ว้าว...";
					mes "S class? สุดยอด...";
					mes "มันจะโหดมากถ้าข้าดึงความสามารถมันออกมาได้.";
					mes "Okay... เลือกอาวุธที่เจ้าต้องการจะเจาะรูสิ ?";
					next;
					switch(select("Zephyrus:Mailbreaker:Dragon Slayer:Swordbreaker:Assasin Dagger:Grand Cross:Executioner"))
					{
						case 1: callfunc "Func_Socket2",1468, 1481,40,51,1000,984,5,999,10;
						case 2: callfunc "Func_Socket2",1225,13032,40,51,1000,984,5,999,10;
						case 3: callfunc "Func_Socket2",1166, 1180,40,51,1000,984,5,999,10;
						case 4: callfunc "Func_Socket2",1224,13031,40,51,1000,984,5,999,10;
						case 5: callfunc "Func_Socket2",1232,13033,40,51,1000,984,5,999,10;
						case 6: callfunc "Func_Socket2",1528, 1540,40,51,1000,984,5,999,10;
						case 7: callfunc "Func_Socket2",1169, 1179,40,51,1000,984,5,999,10;
					}
			}
		case 2:
			mes "[Leablem]";
			mes "Armor, จริงดิ ?";
			mes "ได้เลย, ไม่มีปัญหา.";
			mes "ข้าได้แบ่งเกราะออกเป็น 4 คลาส, ตามระดับความยากง่าย.";
			mes "C Class ทำง่ายสุด, และ S Class ทำยากสุด.";
			mes "เลือกคลาสที่เจ้าต้องการทำสิ ?";
			next;
			switch(select("C Class:B Class:A Class:S Class"))
			{
				case 1:
					mes "[Leablem]";
					mes "C Class Armor, เอ้อ ?";
					mes "ก็โอน่ะ.";
					mes "เลือกเกราะที่เจ้าต้องการจะเจาะรูสิ ?";
					next;
					switch(select("Sunflower:Ph.D Hat:Big Ribbon:Boys Cap"))
					{
						case 1: callfunc "Func_Socket2",2253,5351,40,66,100,999,3;
						case 2: callfunc "Func_Socket2",5012,5347,40,66,100,999,3;
						case 3: callfunc "Func_Socket2",2244,5348,40,66,100,999,5;
						case 4: callfunc "Func_Socket2",5016,5349,40,66,100,999,5;
					}
				case 2:
					mes "[Leablem]";
					mes "เจ้าเลือก B Class.";
					mes "งั้น, เกราะไหนล่ะที่เจ้ามี ?";
					next;
					switch(select("Skull Ring:High Heels"))
					{
						case 1: callfunc "Func_Socket2",2609,2715,40,61,300,999,5;
						case 2: callfunc "Func_Socket2",2409,2432,40,61,300,999,5;
					}
				case 3:
					mes "[Leablem]";
					mes "อูวว, A Class.";
					mes "ค่อนข้างแพงเอาเรื่อง!";
					mes "ได้เลย, เลือกเกราะที่เจ้าต้องการจะเจาะรูสิ ?";
					next;
					switch(select("Pirate Bandana:Black Leather Boots"))
					{
						case 1: callfunc "Func_Socket2",2287,5350,50,61,500,985,1; // 50,61 - it's not a typo
						case 2: callfunc "Func_Socket2",2425,2434,40,51,500,985,1;
						//case 3: callfunc "Func_Socket2",15053,15056,x,x,x,x,x;	// Special Ninja Suit (Needs more info)
					}
				case 4:
					mes "[Leablem]";
					mes "อืม... S Class?";
					mes "เจ้าแน่ใจน่ะ ?";
					mes "ได้เลย... บอกข้ามาเลยว่าเจ้าจะเจาะรูเกราะชิ้นไหน, และข้าจะทำตามความคาดหวังของเจ้าเอง.";
					next;
					switch(select("Mage Coat:Holy Robe:Sacred Mission:Undershirt:Pantie"))
					{
						case 1: callfunc "Func_Socket2",2334,2372,40,51,1000,985,1; // Mage Coat has 10% success chance
						case 2: callfunc "Func_Socket2",2327,2373,45,51,1000,985,1; // the rest has 5%
						case 3: callfunc "Func_Socket2",2111,2128,45,51,1000,985,1;
						case 4: callfunc "Func_Socket2",2522,2523,45,51,1000,985,1;
						case 5: callfunc "Func_Socket2",2339,2371,45,51,1000,985,1;
					}
			}
		case 3:
			mes "[Leablem]";
			mes "Oh, it's nothing special, but he thinks";
			mes "it's not something for humans to mess with. ";
			mes "I don't want to upset my brother,";
			mes "so I'd better forget about it.";
			next;
			mes "[Leablem]";
			mes "...That service itself has a very high success chance,";
			mes "but requires a lot of materials and a high-rate service charge.";
			mes "I highly doubt that anyone would actually want to use";
			mes "the service, even if I did open it.";
			next;
			mes "[Leablem]";
			mes "...Ummm...";
			next;
			mes "[Leablem]";
			mes ".............";
			next;
			mes "[Leablem]";
			mes ".............";
			next;
			mes "[Leablem]";
			mes "You seem interested in using that service.";
			mes "If you want, I can at least tell you about it.";
			mes "But you must promise to keep this a secret.";
			mes "If my brother finds out what I'm about to tell you,";
			mes "he's gonna give me a beating!";
			next;
			mes "[Leablem]";
			mes "This special service has a 90% success chance,";
			mes "but requires 2 Gold and 2 hundred million zeny:";
			mes "I can add slots to the Hat of the Sun God.";
			next;
			mes "[Leablem]";
			mes "My brother said that, even since ancient times, it's forbidden to modify that hat.";
			mes "But I believe humans are too curious not to";
			mes "experiment, especially when it's forbidden.";
			mes "I just don't want my brother to yell at me. That's all.";
			next;
			switch(select("Use the Service","Quit"))
			{
				case 1:
					mes "[Leablem]";
					mes ".................";
					next;
					mes "[Leablem]";
					mes ".................";
					next;
					mes "[Leablem]";
					mes "...Wh... What? No!";
					mes "Giving me that dirty look";
					mes "won't make me do it!";
					mes "No, I said no! No is no! No!";
					next;
					mes "[Leablem]";
					mes "No, I won't! No!";
					next;
					mes "[Leablem]";
					mes "I can't do it...";
					next;
					mes "[Leablem]";
					mes "I can't do... It...";
					next;
					mes "[Leablem]";
					mes ".................";
					next;
					mes "[Leablem]";
					mes "Umm...";
					next;
					mes "[Leablem]";
					mes "You're so persistent. Okay, I'll make you an exception.";
					mes "Just don't tell anyone,";
					mes "especially my brother. Do you promise?";
					next;
					mes "[Leablem]";
					mes "Okay then, please bring me";
					mes "^FF00002 Gold, 2 hundred million zeny -- the service charge --,";
					mes "^FF0000and a Hat of the Sun God.";
					mes "^FF0000You know your chance of success is 90% with this, don't you?^000000";
					mes "We don't have time to waste,";
					mes "so let's get down to business right away.";
					next;
					if ((Zeny > 199999999) && (countitem(969) > 1) && (countitem(5022) > 0))
					{
						mes "[Leablem]";
						mes "Shall we start now?";
						mes "Pray to your gods for good luck.";
						next;
						set .@a, rand(1,100);
						if ((.@a > 4) && (.@a < 95))
						{
							specialeffect EF_LORD;
							mes "[Leablem]";
							mes "Excellent! Wow, I guess the 90% success chance is true!";
							mes "Look, the slot was added in the perfect place.";
							mes "Congratulations.";
							delitem 5022,1; //Helm_Of_Sun
							delitem 969,2; //Gold
							set Zeny, Zeny - 200000000;
							getitem 5353,1; //Helm_Of_Sun_
							next;
							mes "[Leablem]";
							mes "This is the first and last time I will do this for you.";
							mes "Don't ever tell anyone";
							mes "about this. Okay?";
							close;
						}
						else
						{
							specialeffect EF_SUI_EXPLOSION;
							mes "[Leablem]";
							mes "Wah! Oh my god, we failed!";
							mes "My god, I guess we had the worst kind of luck...";
							mes "What should we do? ...This is why";
							mes "I didn't want to do in the first place!";
							delitem 5022,1; //Helm_Of_Sun
							delitem 969,2; //Gold
							set Zeny, Zeny - 200000000;
							next;
							mes "[Leablem]";
							mes "No, don't ever ask me to do such a risky thing again!";
							mes "Bah~";
							close;
						}
					}
					else
					{
						mes "[Leablem]";
						mes "Umm...";
						next;
						mes "[Leablem]";
						mes "You don't have enough materials.";
						mes "Man, why did you even ask me to try?";
						mes "I'm not going to do this again for you,";
						mes "even if you bring enough materials. Bah~";
						close;
					}
				case 2:
					mes "[Leablem]";
					mes "Don't ever tell anyone";
					mes "about what I told you. Okay?";
					close;
			}
		case 4:
			mes "[Leablem]";
			mes "ทำให้มันง่าย, นักผจญภัย.";
			mes "ถ้าเจ้าสนใจที่จะเจาะรู, ให้กลับมาหาข้า.";
			mes "แล้วเจอกันใหม่~";
			close;
	}
}

function	script	Func_Socket2	{
	.@item_id = getarg(0);
	.@zeny_req = getarg(4);
	setarray .@item_req[0], getarg(5), getarg(7,0);
	setarray .@req_amount[0], getarg(6), getarg(8,0);

	disable_items;
	mes "[Leablem]";
	mes "เจ้าต้องการที่จะเจาะ "+ getitemname(.@item_id) +"?";
	mes "ได้เลย, โปรดนำ ^FF0000"+ .@req_amount[0] +" "+ getitemname(.@item_req[0]) + (.@req_amount[0] > 1 && .@item_req[0] != 999 ? "s":"") +"^000000, "+ (.@item_req[1] != 0 && .@req_amount[1] != 0 ? ("^FF0000"+ .@req_amount[1] +" "+ getitemname(.@item_req[1]) + (.@req_amount[1] > 1 && .@item_req[1] != 999 ? "s":"") + "^000000 ") : "") + "และ ^FF0000" + (.@zeny_req >= 1000 ? (.@zeny_req /1000 +",000") : .@zeny_req) +",000 zeny^000000 เป็นค่าบริการ.";
	mes "อาห์, และอย่าลืม "+ getitemname(.@item_id) +"!";
	next;
	mes "[Leablem]";
	if (getiteminfo(.@item_id, ITEMINFO_LOCATIONS) & EQP_HAND_R) // weapon
	{
		mes "ข้าสามารถเจาะรูตอนนี้ได้เลยถ้าเจ้ามีของและเงิน.";
		mes "อย่างไรก็ตาม, เจ้าต้องรู้ไว้ว่าข้ามีโอกาสทำของแตกได้.";
		mes "ข้าคิดว่ามันแฟร์ดีที่จะเตือนเจ้าไว้...";
		next;
		mes "[Leablem]";
		mes "ถ้าการเจาะรูล้มเหลว, มันจะทำให้ ^FF0000อาวุธ^000000, ^FF0000และการ์ดที่ใส่โดนทำลายไปด้วย^000000.";
	}
	else // armor
	{
		mes "ถ้าเจ้ามีแร่, เงินและเกราะพร้อมแล้ว, งั้นพวกเรามาเริ่มเจาะรูกัน.";
		mes "แต่ก่อนหน้านั้น, ข้าต้องเตือนเจ้าก่อน.";
		next;
		mes "[Leablem]";
		mes "ถ้าการเจาะรูล้มเหลว, มันจะทำให้ ^FF0000เกราะ^000000, ^FF0000ค่าบวก^000000 ^FF0000และการ์ดที่ใส่โดนทำลายไปด้วย^000000.";
	}
	mes "เจ้ายังอยากเจาะรูอยู่ไหม ?";
	next;
	switch(select("เจาะรูไอเท็ม:ยกเลิก"))
	{
		case 1:
			if ((Zeny >= .@zeny_req*1000) && countitem(.@item_req[0]) >= .@req_amount[0] && countitem(getarg(7,512)) >= .@req_amount[1] && countitem(.@item_id) > 0)// if there's no getarg(7) and getarg(8) provided, check itemcount against 0 (we still need a valid item ID though)
			{
				mes "[Leablem]";
				mes "ได้เลย, มาเริ่มงานกัน!";
				mes "เจ้าจะภาวนาให้สำเร็จก็ได้น่ะ.";
				next;
				set .@a, rand(1,100);
				if ((.@a > getarg(2)) && (.@a < getarg(3)))
				{
					specialeffect getarg(3) == 51 ? EF_LORD : EF_SANCTUARY; // only S Class items have Lord of Vermillion effect
					mes "[Leablem]";
					mes "เยี่ยม, มันสำเร็จ.";
					mes "มันดูสุดยอดมากๆ!";
					delitem .@item_id,1;
					delitem .@item_req[0],.@req_amount[0];
					if (.@item_req[1] != 0 && .@req_amount[1] != 0)
						delitem .@item_req[1],.@req_amount[1];
					Zeny = Zeny - .@zeny_req *1000;
					getitem getarg(1),1;
					next;
					mes "[Leablem]";
					mes "แล้วพบกันใหม่, เจ้าเกลอ!";
					close;
				}
				else
				{
					specialeffect EF_SUI_EXPLOSION;
					mes "[Leablem]";
					mes "หว่า! ...ข้าขอโทษ, มันล้มเหลว.";
					mes "อย่างไรก็ตาม, ข้าว่ามันปกติแล้ว.";
					mes "เพราะเจ้ามันไม่มีโชคเอง, หรือว่าไม่ใช่ ?";
					mes "อย่าเศร้าไปเลย, ลองใหม่ครั้งหน้า.";
					delitem .@item_id,1;
					delitem .@item_req[0],.@req_amount[0];
					if (.@item_req[1] != 0 && .@req_amount[1] != 0)
						delitem .@item_req[1],.@req_amount[1];
					Zeny = Zeny - .@zeny_req *1000;
					next;
					mes "[Leablem]";
					mes "ข้าจะอวยพรให้ในครั้งหน้า!";
					close;
				}
			}
			else
			{
				mes "[Leablem]";
				mes "ข้าอยากจะเจาะรูน่ะ, แต่เจ้ามีของไม่ครบ.";
				mes "เจ้าอาจจะขาดของสวมใส่ แร่ หรือเงิน ?";
				close;
			}
		case 2:
			mes "[Leablem]";
			mes "ต้องการเวลาคิดสักหน่อย, หรอ ?";
			mes "ได้เลย, ข้าเข้าใจดี.";
			mes "แต่ชีวิตมันจะไม่สนุกถ้าเจ้าเอาแต่เล่นแบบปลอดภัย~";
			close;
	}
}

payon,236,199,3	duplicate(SocketEnchant2)	Leablem#pay	86
lighthalzen,96,137,3	duplicate(SocketEnchant2)	Leablem#lhz	86
prt_in,22,60,6	duplicate(SocketEnchant2)	Leablem#prt	86
morocc,49,39,4	duplicate(SocketEnchant2)	Leablem#moc	84
