//===== rAthena Script =======================================
//= Reward_OBT
//===== By: ==================================================
//= DurexzOfficial
//===== Current Version: =====================================
//= Ver.1.0 - Create npc Register CBT [DurexzOfficial#5799]
//= Ver.1.2 - Create npc Reword OBT [DurexzOfficial#5799]
//= Ver.1.3 - Add Open and Close reward cash or zeny [DurexzOfficial#5799]

/*= Add SQL ==================================================
CREATE TABLE IF NOT EXISTS `reward_obt` (
  `id` int(11) NOT NULL AUTO_INCREMENT,
  `account_id` int(11) NOT NULL,
  `status` tinyint(1) NOT NULL,
  `time` datetime NOT NULL,
  PRIMARY KEY (`id`)
) ENGINE=MyISAM DEFAULT CHARSET=utf8;
*/
//===== Compatible With: =====================================
//= rAthena Project
//============================================================

morocc,152,90,4	script	Reward_CBT	456,{
	mes .npc$;
	mes "สวัสดีผู้เล่น ^0000FF"+strcharinfo(0)+"^000000";
	mes "ต้องการลงทะเบียนใช่หรือไม่...??";
	
	if(select("~ ยกเลิก:~ ตกลง") == 1){
		next;
		mes "ไว้พบกันใหม่คราวหลัง...!!";
		close;
	}
	next;
	if(BaseLevel < .level ){
		mes "เลเวลของท่าน ยังไม่ถึงที่กำหนด";
		close;
	}

	if( Class < .classes ){	// check class @job
		mes "อาชีพของท่านยังไม่เพียงพอ";
		close;
	}
	if(#cbt){
		mes "ท่านเคยลงทะเบียนแล้ว";
		close;
	}
	query_sql("INSERT INTO `reward_obt`(`account_id`,`status`,`time`) VALUES ('"+getcharid(3)+"',1,now())");
	#cbt = 1;
	mes "ท่านลงทะเบียนเรียบร้อยแล้ว";
	end;

OnInit:
	.npc$ = "[ ลงทะเบียน CBT ]";
	.level = 99;		// Level ที่สามารถลงทะเบียนได้
	.classes = 4001;	// เช็คอาชีพถ้าน้อยกว่า 4001 ไม่ได้ @job < low 4001 = High Novice
	end;
}

morocc,155,90,4	script	Reward_OBT	456,{
	mes .npc$;
	mes "สวัสดีผู้เล่น ^0000FF"+strcharinfo(0)+"^000000";
	mes "ท่านต้องการรับรางวัลใช่หรือไม่...!!";
	
	if(select("~ ยกเลิก:~ ตกลง") == 1){
		next;
		mes "ไว้พบกันใหม่คราวหลัง...!!";
		close;
	}
	next;
	query_sql("SELECT `account_id`,`status` FROM `reward_obt` WHERE status = 1 AND `account_id` = '"+getcharid(3)+"'",.accid,.status);
	if (.accid != getcharid(3) && .status != 1){
		mes "ท่านไม่ได้ลงทะเบียนตอน CBT ไว้";
		close;
	}
	query_sql("SELECT `status` FROM `reward_obt` WHERE status = 2 AND `account_id` = '"+getcharid(3)+"'",.status);
	if(.status == 2){
		mes "ท่านเคยรับรางวัลไปแล้ว";
		close;
	}
	query_sql("UPDATE `reward_obt` SET `status` = 2 WHERE status = 1 AND `account_id` ="+getcharid(3));
	
	if(.cash){
		#CASHPOINTS += .cash_qty;
		dispbottom "ท่านได้รับ "+callfunc("F_InsertComma",.cash_qty)+" Cashpoints รวมเป็น "+callfunc("F_InsertComma",#CASHPOINTS)+" Cashpoints.";
	}
	if(.zeny){
		Zeny += .zeny_qty;
		dispbottom "ท่านได้รับ "+callfunc("F_InsertComma",.zeny_qty)+" Zeny รวมเป็น "+callfunc("F_InsertComma",Zeny)+" Zeny.";
	}
	for(.i=0; .i < getarraysize(.item); .i+=2){
		getitem .item[.i],.item[.i+1];
	}
	end;

OnInit:
	.npc$ = "[ รับรางวัลจาก CBT ]";
	.cash = true;					// ได้รับ CashPoint true = เปิด ใช้งาน , false = ปิดใช้งาน
	.cash_qty = 10000;				// จำนวน CashPoint ที่ได้รับ
	.zeny = true;					// ได้รับ Zeny true = เปิด ใช้งาน , false = ปิดใช้งาน
	.zeny_qty = 1000000;			// จำนวน Zeny ที่ได้รับ
	setarray .item,501,10,502,10;	// รหัสไอเท็ม,จำนวนไอเท็ม,รหัสไอเท็ม,จำนวนไอเท็ม,...,....
	end;
}