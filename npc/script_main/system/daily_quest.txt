//-------------------------------------------------------
// Daily Quest by Durexz
//-------------------------------------------------------
-	pointshop	daily_shop_points	-1,#daily_points,501:-1

morocc,153,82,5	script	Daily Quest	4_F_DRKAFRA01,{
	doevent strnpcinfo(0) + "::OnDailyQuest";
	end;
	
	OnInit:
		.NPCname$ = "[^0000FF "+strnpcinfo(0)+" ^000000]";
		waitingroom "  Daily Quest",0;
		.sb$ = ">";
		//-------------------------------------------------------
		// Configs.
		//-------------------------------------------------------
		.quest_count = 1;		// Daily Quest Count
		.min_hunt = 30;			// Min Hunting Quest Count
		.max_hunt = 50;		// Max Hunting Quest Count
		.min_Shunt = 10;			// Min Hunting Quest Count
		.max_Shunt = 20;		// Max Hunting Quest Count
		.min_collect = 30;		// Min Collect Quest Count
		.max_collect = 50;		// Max Collect Quest Count
		
		// MVP List.
		setarray .s_list,1243,1219,1375,1098,1736;
		// Mob List.
		setarray .hunt_list,1002,1141,1066,1044,1145,1063,1269,1160,1026,1257,1032,1164,1071,1495,1278,1616,1122,1033,1127,1166,1165,1113,1019,1025,1619;
		// Item List.
		setarray .collect_list,909,1052,910,950,1017,949,1095,955,901,7053,929,958,932,7201,7004,7318,911,907,1055,1028,1056,938,925,926,928;
		
		//-------------------------------------------------------
		// @dailyquest : Command for Daily Quest.
		//-------------------------------------------------------
		bindatcmd "dailyquest",strnpcinfo(0)+"::OnDailyQuest";
		
		//-------------------------------------------------------
		// Shop Reward. "Cost:ID"
		//-------------------------------------------------------
		setarray .daily_reward$,
			"1:12263",
			"1:12210",
			"1:12412",
			"1:30001",
			"5:7619",
			"5:7620",
			"5:12214",
			"5:12103",
			"5:23209",
			"5:14522",
			"21:14523",
			"28:60059";


		.size = getarraysize(.daily_reward$);
		for(.@i = 0; .@i < .size; .@i++) {
			explode(.@clvl$,.daily_reward$[.@i],":");
			npcshopadditem "daily_shop_points",atoi(.@clvl$[1]),atoi(.@clvl$[0]);
		}
		npcshopdelitem "daily_shop_points",501;
		// SQL auto create.
		query_sql("CREATE TABLE IF NOT EXISTS `daily_quest` (`id` INT(11) UNSIGNED NOT NULL AUTO_INCREMENT,`account_id` INT(11) UNSIGNED NOT NULL DEFAULT '0',`quest` INT(11) UNSIGNED NOT NULL DEFAULT '"+.quest_count+"',PRIMARY KEY (`id`)) ENGINE=MyISAM DEFAULT CHARSET=utf8");
	end;

OnPCLoginEvent:
		if (gettime(DT_DAYOFMONTH) != #reward_date) {
			#reward_date = gettime(DT_DAYOFMONTH);
			data_quest$ = "";
		}
	end;

OnNPCKillEvent:
		if (!c_hunt) end;
		explode(.@T$,data_quest$,":");
		for(.@i = 0; .@i < .quest_count; .@i++) {
			explode(.@TT$,.@T$[.@i],",");
			.@count = atoi(.@TT$[3]);
			if (atoi(.@TT$[0]) == 2 || atoi(.@TT$[0]) == 4 || atoi(.@TT$[1]) != killedrid || atoi(.@TT$[3]) >= atoi(.@TT$[2]))
				continue;
			.@count += 1;
			.@TT$[3] = .@count;
			.@T$[.@i] = implode(.@TT$,",");
			data_quest$ = implode(.@T$,":");	
			dispbottom "Daily Quest สถานะการล่า : "+getmonsterinfo(atoi(.@TT$[1]),MOB_NAME)+" ["+.@count+" / "+.@TT$[2]+"]",0xFFA500;
			if (.@count == atoi(.@TT$[2]))
				dispbottom "Daily Quest สถานะการล่า : "+getmonsterinfo(atoi(.@TT$[1]),MOB_NAME)+" [เสร็จสิ้น]",0xFFA500;
		}
	end;

OnDailyQuest:
		disable_items;		
		.@account_id = getcharid(3);
		if (!query_sql("SELECT `id` FROM `daily_quest` WHERE `account_id` = '"+.@account_id+"'",.@sql_id)) {
			mes .NPCname$;
			mes "จะเปิดใช้งานหรือไม่?";
			if(select("เปิด:ไม่เปิด") == 2 ) end;
			doevent strnpcinfo(0)+"::OnActivate";
			end;
		}
	query_sql("SELECT `quest` FROM `daily_quest` WHERE `id` = '"+.@sql_id+"'",.@quest);
		
		.@prefix$ = .@quest > 0 ? "^32CD32":"^DC143C";
		mes .NPCname$;
		mes "ยินดีต้อนรับสู่ ^FF0000Daily Quest^000000";	
		mes "ภารกิจที่เหลืออยู่ - ["+.@prefix$+""+.@quest+"^000000/"+.quest_count+"]";
		mes "ทำอะไรดี";
		next;
		switch(select(((.@quest > 0)?""+.sb$+" จัดการเควส":""),""+.sb$+" ตรวจสอบรางวัลถัดไป",""+.sb$+" ข้อมูล",""+.sb$+" ออก")) {
			case 1:
			OnQuest:
				.@menu$ = "";
				.@q = 0;
				explode(.@T$,data_quest$,":");
				for(.@i = 0; .@i < .quest_count; .@i++) {
					if (.@T$[.@i] == "")
						.@menu$ += "^777777ยังไม่ได้เลือกเควส^000000" + ":";
					else {
						explode(.@TT$,.@T$[.@i],",");
						switch(atoi(.@TT$[0])) {
							case 1: 
								if (atoi(.@TT$[3]) >= atoi(.@TT$[2]))
									.@status$ = "^00FF00เสร็จสิ้น^000000";
								else
									.@status$ = "^0000CDกำลังทำ^000000";
								.@quest$ = "ล่ามอนสเตอร์["+.@status$+"]"; 
								break;
								
							case 2:
								if (countitem(atoi(.@TT$[1])) >= atoi(.@TT$[2]))
									.@status$ = "^00FF00เสร็จสิ้น^000000";
								else
									.@status$ = "^0000CDกำลังทำ^000000";
								.@quest$ = "เก็บรวบรวมไอเทม[" + .@status$ + "]"; 
								break;
								
							case 3: 
								if (atoi(.@TT$[3]) >= atoi(.@TT$[2]))
									.@status$ = "^00FF00เสร็จสิ้น^000000";
								else
									.@status$ = "^0000CDกำลังทำ^000000";
								.@quest$ = "ล่ามอนสเตอร์ พิเศษ["+.@status$+"]";						
								break;
								
							case 4: .@quest$ = "^0000CDเสร็จสิ้น^000000"; break;
						}
						.@menu$ += .@quest$ + ":";
					}
				}
				mes .NPCname$;
				mes "เลือกเควสที่จะจัดการ.";
				next;
				.@menu$ += "ออก";
				.@s = select(.@menu$) - 1;
				if (.@s == .quest_count)
					end;
				explode(.@TT$,.@T$[.@s],",");
				if (atoi(.@TT$[0]) == 4) {
					mes .NPCname$;
					mes "คุณได้เสร็จสิ้นภารกิจสำหรับช่องนี้แล้ว.";
					next;
					goto OnQuest;
				}
				if (.@T$[.@s] == "") {
					mes .NPCname$;
					mes "เลือกเควสที่จะเพิ่มในช่องเควสนี้.";
					next;
					switch(select(
						""+.sb$+" สุ่ม"
					)) {
						default: .@type = rand(1,3); break;
					}
					
					switch(.@type) {
						case 1:
							.@id = .hunt_list[rand(getarraysize(.hunt_list))];
							.@amount = rand(.min_hunt,.max_hunt);
							.@type$ = "ล่ามอนสเตอร์";
							.@prefix$ = "ล่า";
							c_hunt++;
							break;
							
						case 2:
							.@id = .collect_list[rand(getarraysize(.collect_list))];
							.@amount = rand(.min_collect,.max_collect);
							.@type$ = "เก็บรวบรวมไอเทม";
							.@prefix$ = "รวบรวม";
							break;
							
						case 3:
							.@id = .s_list[rand(getarraysize(.s_list))];
							.@amount = rand(.min_Shunt,.max_Shunt);
							.@type$ = "ล่ามอนสเตอร์ MVP";
							.@prefix$ = "MVP";
							c_hunt++;
							break;
					}
					.@TT$[0] = .@type;
					.@TT$[1] = .@id;
					.@TT$[2] = .@amount;
					.@TT$[3] = 0;
					.@T$[.@s] = implode(.@TT$,",");
					data_quest$ = implode(.@T$,":");
					mes .NPCname$;
					mes "เควส " + (.@s + 1) + ": " + .@type$;
					mes .@prefix$ + " ID : " + .@id;
					mes .@prefix$ + " ชื่อ : " + ((.@type == 2)?getitemname(.@id):getmonsterinfo(.@id,MOB_NAME));
					if (.@type < 3)
						mes .@prefix$ + " จำนวน : " + .@amount;
					next;
					goto OnQuest;
				}
				.@type = atoi(.@TT$[0]);
				switch(.@type) {
					case 1:
						.@id = atoi(.@TT$[1]);
						.@amount = atoi(.@TT$[2]);
						.@type$ = "ล่ามอนสเตอร์";
						.@prefix$ = "ล่า";
						break;
						
					case 2:
						.@id = atoi(.@TT$[1]);
						.@amount = atoi(.@TT$[2]);
						.@type$ = "เก็บรวบรวมไอเทม";
						.@prefix$ = "รวบรวม";
						break;
						
					case 3:
						.@id = atoi(.@TT$[1]);
						.@amount = atoi(.@TT$[2]);
						.@type$ = "ล่ามอนสเตอร์ พิเศษ";
						.@prefix$ = "พิเศษ";
						break;
				}
				mes .NPCname$;
				mes "เควส " + (.@s + 1) + ": " + .@type$;
				mes .@prefix$ + " ID : " + .@id;
				mes .@prefix$ + " ชื่อ : " + ((.@type == 2)?getitemname(.@id):getmonsterinfo(.@id,MOB_NAME));
				if (.@type < 3)
					mes .@prefix$ + " จำนวน : " + .@amount;
				next;
				if (.@type == 2) {
					if (countitem(.@id) >= .@amount)
						.@pass = true;
				} else {
					if (atoi(.@TT$[3]) >= .@amount)
						.@pass = true;
				}
				switch(select(((.@pass > 0)?"ส่งเควส":""),"ออก")) {	
					case 1:
						if (.@type == 2)
							delitem .@id,.@amount;
						else
							c_hunt--;
						.@T$[.@s] = "4,0,0,0";
						data_quest$ = implode(.@T$,":");
						.@q = .@quest - 1;
						query_sql("UPDATE `daily_quest` SET `quest` = '"+.@q+"' WHERE `id` = '"+.@sql_id+"'");
						next;
						mes .NPCname$;
						mes "ส่งเควสแล้ว.";
						mes "รางวัล  Cash 100 พ้อย";
						#CASHPOINTS += 100;
						#daily_points ++;
						next;
						goto OnQuest;
						
					case 2:
						close;				
				}
				break;
				

			case 2:
				callshop "daily_shop_points",1;
				end;
				
			case 3:
				mes .NPCname$;
				mes "Daily Quest เควสรายวันที่จะได้ฆ่ามอนสเตอร์หรือเก็บ Item เพื่อสำเร็จเควส.";
				mes "Daily Quest ไม่สามารถทำย้อนหลังและเผื่อวันอื่นๆนอกจากวันปัจจุบันได้.";
				end;
				
			case 4:
				end;
		}
	end;

OnActivate:
		.@account_id = getcharid(3);
		if (query_sql("SELECT `id` FROM `daily_quest` WHERE `account_id` = '"+.@account_id+"'")) {
			dispbottom "ระบบ : คุณได้เปิดใช้งาน Daily Quest กับบัญชีนี้แล้ว.",0xCC0000;
			end;
		}
		query_sql("INSERT INTO `daily_quest` (`account_id`,`quest`) VALUES ('"+.@account_id+"','"+.quest_count+"')");
		dispbottom "คุณได้เปิดใช้งาน Daily Quest กับบัญชีนี้แล้ว.";
	end;

	
OnHour00:
		if (gettime(DT_DAYOFMONTH) == 1) {
			query_sql("TRUNCATE `daily_quest`");
			end;
		}
		query_sql("UPDATE `daily_quest` SET `quest` = '"+.quest_count+"' WHERE `quest` = '"+.quest_count+"'");
	end;

}